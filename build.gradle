buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
        classpath 'org.ajoberstar:gradle-git:1.7.1'
    }
}

apply plugin: 'java'
apply plugin: 'jetty'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'org.ajoberstar.grgit'

import org.ajoberstar.grgit.Grgit
String containerVersion = Grgit.open('.')?.head()?.abbreviatedId

if (System.env.BUILD_ID) {
    containerVersion = "${System.env.BUILD_ID}-${containerVersion}"
}

group = 'org.jenkins-ci'
description = 'User self-service account management app'
version = '2.5'
defaultTasks 'container'

repositories {
    jcenter()
    maven { url 'https://repo.jenkins-ci.org/public' }
    mavenLocal()
}

dependencies {
    compile 'javax.servlet:servlet-api:2.4'

    compile 'org.glassfish:javax.json:[1.0.2,1.1)'
    compile 'commons-codec:commons-codec:1.4'

    compile 'org.kohsuke.stapler:stapler:[1.233,2.0)'
    compile('org.kohsuke.stapler:stapler-jelly:[1.203,2.0)')
    compile 'org.kohsuke.stapler:stapler-openid-server:[1.0,2.0)'

    compile 'org.jvnet.hudson:commons-jelly-tags-define:1.0.1-hudson-20071021'

    compile 'javax.mail:mail:[1.4,2.0)'
    compile 'javax.activation:activation:1.1.1'

    compile('io.jenkins.backend:jira-rest-ldap-syncer:1.1') {
        exclude module: 'javamail'
    }

    testCompile 'junit:junit:[4.8.1,5.0)'
}

jettyRun {
    contextPath '/account'
    httpPort 8081
}

war {
    dependsOn check
}

task container(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    description 'Build the account-app container'
    inputDir projectDir
    tag "jenkinsciinfra/account-app:${containerVersion}"
    dependsOn war
}

task pushContainer(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    description 'Publish the account-app container'
    imageName 'jenkinsciinfra/account-app'
    tag containerVersion
    dependsOn container
}
